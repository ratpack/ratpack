/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = "The core HTTP and composition mechanisms of Ratpack"

apply from: "$rootDir/gradle/javaModule.gradle"

ext.apiLinks = [
    "http://netty.io/4.0/api/",
    "http://docs.oracle.com/javase/7/docs/api/",
    "http://docs.guava-libraries.googlecode.com/git-history/v${commonVersions.guava}/javadoc/"
]

repositories {
  maven { url "http://repo.akka.io/snapshots/" } // for reactive streams TCK
}

dependencies {
  compile "io.netty:netty-codec-http:$commonVersions.netty"
  compile "com.google.guava:guava:$commonVersions.guava"
  compile commonDependencies.slf4j
  compile 'org.reactivestreams:reactive-streams:0.4.0.M2'

  compile 'org.javassist:javassist:3.18.1-GA'

  testCompile 'org.apache.commons:commons-lang3:3.3.2'
  testCompile 'org.reactivestreams:reactive-streams-tck:0.4.0.M2-20140904-173944-SNAPSHOT'
  testCompile 'org.mockito:mockito-core:1.9.5'
}

test {
  maxParallelForks = 3
}

// Reactive Streams TCK uses TestNG, and Gradle can't run TestNG and JUnit in the same run.
task streamTckTest(type: Test) {
  tasks.check.dependsOn it

  useTestNG()
  scanForTestClasses = false
  include "**/stream/tck/*"

  classpath = sourceSets.test.runtimeClasspath
  testClassesDir = sourceSets.test.output.classesDir
  testSrcDirs = sourceSets.test.allSource.srcDirs.toList()

  binResultsDir = file("$buildDir/$name-results/binary")
  reports {
    html.destination = file("$buildDir/reports/$name")
    junitXml.destination = binResultsDir.parentFile
  }

  // Not passing the tck yet
  ignoreFailures = true
}