/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = "Ratpack integration for the Groovy programmming language - http://groovy-lang.org"

apply from: "$rootDir/gradle/groovyModule.gradle"

ext.apiLinks = [
    "http://docs.groovy-lang.org/${commonVersions.groovy}/html/api/",
    "http://google.github.io/guava/releases/${commonVersions.guava}/api/docs"
]

dependencies {
  api project(":ratpack-core")
  api project(":ratpack-guice")

  implementation "org.javassist:javassist:${commonVersions.javassist}"
  testImplementation project(":ratpack-test")
}

task writeGroovyVersionFile {
  def versionFile = file("$buildDir/ratpack-groovy-version/ratpack/minimum-groovy-version.txt")
  inputs.property "groovyVersion", commonVersions.groovy
  outputs.dir versionFile.parentFile.parentFile

  doFirst {
    assert versionFile.parentFile.mkdirs() || versionFile.parentFile.directory
    versionFile.text = commonVersions.groovy
  }
}

sourceSets {
  main {
    resources {
      srcDirs += [
        tasks.named("writeGroovyVersionFile")
      ]
    }
  }
}

// Add implicit dependency due to https://github.com/gradle/gradle/issues/18563
//tasks.named("sourcesJar").configure {
//  it.from(tasks.named("processResources"))
//}
