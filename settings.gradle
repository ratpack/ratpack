/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id "com.gradle.enterprise" version "3.10"
  id "com.gradle.common-custom-user-data-gradle-plugin" version "1.6.5"
}

enableFeaturePreview("GROOVY_COMPILATION_AVOIDANCE")

apply from: 'gradle/buildScan.gradle'

include \
    "ratpack-benchmark",
    "ratpack-config",
    "ratpack-consul",
    "ratpack-core",
    "ratpack-dropwizard-metrics",
    "ratpack-exec",
    "ratpack-func",
    "ratpack-gradle",
    "ratpack-groovy",
    "ratpack-groovy-test",
    "ratpack-gson",
    "ratpack-guice",
    "ratpack-session",
    "ratpack-session-redis",
    "ratpack-gradle",
    "ratpack-handlebars",
    "ratpack-hikari",
    "ratpack-h2",
    "ratpack-jdbc-tx",
    "ratpack-manual",
    "ratpack-newrelic",
    "ratpack-reactor",
    "ratpack-retrofit2",
    "ratpack-rocker",
    "ratpack-rx2",
    "ratpack-session",
    "ratpack-session-redis",
    "ratpack-session-serialization-kryo",
    "ratpack-site",
    "ratpack-spring-boot",
    "ratpack-spring-boot-starter",
    "ratpack-test",
    "ratpack-test-internal",
    "ratpack-thymeleaf3"

include "perf"

rootProject.name = 'ratpack'

def setBuildFile(project) {
  project.buildFileName = "${project.name}.gradle"
  project.children.each {
    setBuildFile(it)
  }
}

setBuildFile(rootProject)

buildCache {
  remote(HttpBuildCache) {
    url = 'https://ge.ratpack.io/cache/'
    def cacheUsername = System.getenv('GRADLE_ENTERPRISE_CACHE_USERNAME')
    def cachePassword = System.getenv('GRADLE_ENTERPRISE_CACHE_PASSWORD')
    if (cacheUsername && cachePassword) {
      push = true
      credentials {
        username = cacheUsername
        password = cachePassword
      }
    }
  }
}
