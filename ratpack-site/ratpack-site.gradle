/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import org.gradle.plugins.javascript.coffeescript.CoffeeScriptCompile
import ratpack.gradle.WriteTestConfig

import static org.apache.commons.io.FileUtils.copyURLToFile

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.github.robfletcher:compass-gradle-plugin:2.0.5"
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
  }
}

import static org.apache.commons.lang.SystemUtils.*

apply plugin: 'coffeescript-base'
apply plugin: "com.github.robfletcher.compass"
apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
  maven { url "https://oss.jfrog.org/repo" }
  maven { url "http://repo.gradle.org/gradle/javascript-public/" } // for coffeescript plugin
}

ext {
  manualVersions = [
    "0.9.0", "0.9.1", "0.9.2",
    "0.9.3", "0.9.4", "0.9.5",
    "0.9.6", "0.9.7", "0.9.8",
    "0.9.9", "0.9.10", "0.9.11",
    "0.9.12", "0.9.13", "0.9.14",
    "0.9.15", "0.9.16"
  ]
}

configurations {
  latestManual {
    // Force resolve of latest snapshot
    resolutionStrategy {
      cacheChangingModulesFor 0, 'seconds'
    }
  }

  newrelicAgent {}
  browserTestCompile.extendsFrom testCompile
  browserTestRuntime.extendsFrom testRuntime

  manualVersions.each {
    delegate."manual-$it"
  }
}

ext {
  generatedTestResourcesDir = file("$buildDir/generated-test-resources")
}

sourceSets {
  browserTest {
    resources.srcDir(file('src/ratpack'))
    compileClasspath += (main.output + test.output)
    runtimeClasspath += (main.output + test.output)
  }
}

dependencies {
//  springloaded "org.springframework:springloaded:1.2.1.RELEASE"
  compile project(":ratpack-groovy")
  compile project(":ratpack-codahale-metrics")
  compile project(":ratpack-jackson")
  compile project(":ratpack-remote")
  compile project(":ratpack-rx")
  compile project(":ratpack-newrelic")
  compile "org.pegdown:pegdown:${commonVersions.pegdown}"

  compass commonDependencies.compass

  runtime "org.apache.logging.log4j:log4j-slf4j-impl:${commonVersions.log4j}"
  runtime "org.apache.logging.log4j:log4j-api:${commonVersions.log4j}"
  runtime "org.apache.logging.log4j:log4j-core:${commonVersions.log4j}"
  runtime 'com.lmax:disruptor:3.3.2'

  testCompile project(":ratpack-groovy-test")
  testCompile project(":ratpack-remote-test")
  testCompile project(":ratpack-test-internal")
  testCompile commonDependencies.spock
  testCompile 'org.jsoup:jsoup:1.8.1'
  latestManual isHeroku ? "io.ratpack:ratpack-manual:$version" : project(":ratpack-manual")

  browserTestCompile "org.gebish:geb-spock:0.10.0"
  browserTestCompile "org.seleniumhq.selenium:selenium-chrome-driver:2.45.0", {
    exclude module: 'guava'
  }

  manualVersions.each {
    delegate."manual-$it" "io.ratpack:ratpack-manual:$it@zip"
  }

  newrelicAgent "com.newrelic.agent.java:newrelic-agent:${commonVersions.newrelic}"
}

task gatherManuals(type: Sync) {
  inputs.files configurations.latestManual
  into "src/ratpack/manual"
  into version - "-SNAPSHOT", {
    from { zipTree(configurations.latestManual.singleFile) }
  }

  def configureManualExtraction = { configuration, label ->
    inputs.files configuration
    into "src/ratpack/manual"
    into label, {
      from { zipTree(configuration.singleFile) }
    }
  }

  manualVersions.each {
    configureManualExtraction(configurations["manual-$it"], it)
  }

}

idea {
  module {
//    excludeDirs += gatherManuals.destinationDir
  }
}

task downloadChromeDriver {
  def version = "2.10"
  inputs.property("version", version)

  def webdriverOs
  if (IS_OS_MAC) {
    webdriverOs = 'mac32'
  } else if (IS_OS_LINUX) {
    webdriverOs = 'linux64'
  } else if (IS_OS_WINDOWS) {
    webdriverOs = 'win32'
  }

  def output = new File(buildDir, "webdriver/downloads/chromedriver.zip")
  outputs.file output

  doLast {
    copyURLToFile(new URL("http://chromedriver.storage.googleapis.com/${version}/chromedriver_${webdriverOs}.zip"), output)
  }
}

task unzipChromeDriver(type: Copy) {
  dependsOn downloadChromeDriver
  from(zipTree(downloadChromeDriver.outputs.files.singleFile))
  into new File(buildDir, "webdriver/driver")
}

task configureWriteTestConfig {
  dependsOn unzipChromeDriver
  doLast {
    if (!isSnapCi) {
      writeTestConfig.testProperties.putAll(
        "chromedriver.path": fileTree(unzipChromeDriver.outputs.files.singleFile).singleFile.absolutePath
      )
    }
  }
}

task browserTest(type: Test) {
  testClassesDir = sourceSets.browserTest.output.classesDir
  classpath = sourceSets.browserTest.runtimeClasspath

  shouldRunAfter test
  testLogging {
    showStandardStreams true
  }
}

if (isSnapCi || !isCloudCi) {
  check.dependsOn browserTest
}

task writeTestConfig(type: WriteTestConfig) {
  dependsOn configureWriteTestConfig
  generatedTestResourcesDir = project.generatedTestResourcesDir

  testProperties.putAll(
    manualVersions: manualVersions.join(','),
    currentVersion: version
  )
}

sourceSets.test.resources.srcDir generatedTestResourcesDir

processTestResources.dependsOn writeTestConfig

task compileCoffee(type: CoffeeScriptCompile) {
  source fileTree('src/main/static/scripts')
  destinationDir file('src/ratpack/assets/scripts')
}

prepareBaseDir {
  dependsOn gatherManuals, compileCoffee, compassCompile
}

applicationDistribution.into("newrelic") {
  from({ configurations.newrelicAgent }) {
    rename { "agent.jar" }
  }
  from "src/newrelic"
}

jruby {
  gemInstallDir = file("$project.rootProject.buildDir/build/gems")
}

compass {
  gemDir = file("${gradle.gradleUserHomeDir}/ruby-gems")
  cssDir = file('src/ratpack/assets/styles')
  sassDir = file('src/main/static/styles')
  imagesDir = file('src/ratpack/assets/images')
  importPath = files('../ratpack-stylesheets')
  fontsDir = file('../ratpack-stylesheets/fonts')
  relativeAssets = true
  quiet = true
  environment = 'production'
  debugInfo = false
}

run {
  dependsOn compassWatchStart
  finalizedBy compassWatchStop
  main "ratpack.site.SiteMain"
}

mainClassName = "ratpack.site.SiteMain"

task createScriptsDir {
  doLast {
    mkdir "src/ratpack/assets/scripts"
  }
}
configure([compassWatchStart, compassCompile]) {
  dependsOn createScriptsDir
}

clean.dependsOn cleanCompileCoffee
clean.dependsOn cleanGatherManuals

idea {
  module {
    testSourceDirs += sourceSets.browserTest.allSource.srcDirs
    scopes.TEST.plus += [configurations.browserTestCompile]
  }
}

def deployDir = rootProject.file("deploy")

task prepareDeployment(type: Sync) {
  into deployDir
  with distributions.main.contents
}
