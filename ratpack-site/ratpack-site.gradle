/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import ratpack.gradle.WriteTestConfig

buildscript {
  repositories {
    maven { url "http://dl.bintray.com/robfletcher/gradle-plugins" }
    jcenter()
  }
  dependencies {
    classpath 'org.gradle.plugins:gradle-compass:1.0.10'
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.2'
  }
}

import org.gradle.plugins.compass.*
import org.gradle.plugins.javascript.coffeescript.CoffeeScriptCompile

import static org.apache.commons.io.FileUtils.copyURLToFile
import static org.apache.commons.lang.SystemUtils.*

apply plugin: 'coffeescript-base'
apply plugin: 'compass'
apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
  maven { url "https://repo.spring.io/repo" } // for springloaded
  maven { url "http://repo.gradle.org/gradle/javascript-public/" } // for coffeescript plugin
}

ext {
  manualVersions = [
    "0.9.0", "0.9.1", "0.9.2",
    "0.9.3", "0.9.4", "0.9.5",
    "0.9.6", "0.9.7", "0.9.8",
    "0.9.9"
  ]
}

configurations {
  latestManual {
    // Force resolve of latest snapshot
    resolutionStrategy {
      cacheChangingModulesFor 0, 'seconds'
    }
  }

  newrelicAgent {}
  browserTestCompile.extendsFrom testCompile
  browserTestRuntime.extendsFrom testRuntime

  manualVersions.each {
    delegate."manual-$it"
  }
}

ext {
  generatedTestResourcesDir = file("$buildDir/generated-test-resources")
}

sourceSets {
  browserTest {
    resources.srcDir(file('src/ratpack'))
    compileClasspath += (main.output + test.output)
    runtimeClasspath += (main.output + test.output)
  }
}

dependencies {
  springloaded "org.springframework:springloaded:1.2.1.RELEASE"
  compile project(":ratpack-groovy")
  compile project(":ratpack-codahale-metrics")
  compile project(":ratpack-jackson")
  compile project(":ratpack-remote")
  compile project(":ratpack-rx")
  compile project(":ratpack-newrelic")
  compile 'org.pegdown:pegdown:1.4.1'

  runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-rc2'
  runtime 'org.apache.logging.log4j:log4j-api:2.0-rc2'
  runtime 'org.apache.logging.log4j:log4j-core:2.0-rc2'
  runtime 'com.lmax:disruptor:3.2.1'

  testCompile project(":ratpack-groovy-test")
  testCompile project(":ratpack-remote-test")
  testCompile project(":ratpack-test-internal")
  testCompile commonDependencies.spock
  testCompile 'org.jsoup:jsoup:1.7.2'
  latestManual isHeroku ? "io.ratpack:ratpack-manual:$version" : project(":ratpack-manual")

  browserTestCompile "org.gebish:geb-spock:0.9.3"
  browserTestCompile "org.seleniumhq.selenium:selenium-chrome-driver:2.43.1", {
    exclude module: 'guava'
  }

  manualVersions.each {
    delegate."manual-$it" "io.ratpack:ratpack-manual:$it@zip"
  }

  newrelicAgent "com.newrelic.agent.java:newrelic-agent:3.11.0"
}

task gatherManuals(type: Sync) {
  inputs.files configurations.latestManual
  into "src/ratpack/manual"
  into version - "-SNAPSHOT", {
    from { zipTree(configurations.latestManual.singleFile) }
  }

  def configureManualExtraction = { configuration, label ->
    inputs.files configuration
    into "src/ratpack/manual"
    into label, {
      from { zipTree(configuration.singleFile) }
    }
  }

  manualVersions.each {
    configureManualExtraction(configurations["manual-$it"], it)
  }

}

idea {
  module {
//    excludeDirs += gatherManuals.destinationDir
  }
}

task downloadChromeDriver {
  def version = "2.10"
  inputs.property("version", version)

  def webdriverOs
  if (IS_OS_MAC) {
    webdriverOs = 'mac32'
  } else if (IS_OS_LINUX) {
    webdriverOs = 'linux64'
  } else if (IS_OS_WINDOWS) {
    webdriverOs = 'win32'
  }

  def output = new File(buildDir, "webdriver/downloads/chromedriver.zip")
  outputs.file output

  doLast {
    copyURLToFile(new URL("http://chromedriver.storage.googleapis.com/${version}/chromedriver_${webdriverOs}.zip"), output)
  }
}

task unzipChromeDriver(type: Copy) {
  dependsOn downloadChromeDriver
  from(zipTree(downloadChromeDriver.outputs.files.singleFile))
  into new File(buildDir, "webdriver/driver")
}

task configureWriteTestConfig {
  dependsOn unzipChromeDriver
  doLast {
    if (!isSnapCi) {
      writeTestConfig.testProperties.putAll(
        "chromedriver.path": fileTree(unzipChromeDriver.outputs.files.singleFile).singleFile.absolutePath
      )
    }
  }
}

task browserTest(type: Test) {
  testClassesDir = sourceSets.browserTest.output.classesDir
  classpath = sourceSets.browserTest.runtimeClasspath

  shouldRunAfter test
}

if (isSnapCi || !isCloudCi) {
  check.dependsOn browserTest
}

task writeTestConfig(type: WriteTestConfig) {
  dependsOn configureWriteTestConfig
  generatedTestResourcesDir = project.generatedTestResourcesDir

   testProperties.putAll(
     manualVersions: manualVersions.join(','),
     currentVersion: version
   )
}

sourceSets.test.resources.srcDir generatedTestResourcesDir

processTestResources.dependsOn writeTestConfig

ideaModule.dependsOn processTestResources

task compileCoffee(type: CoffeeScriptCompile) {
  source fileTree('src/main/static/scripts')
  destinationDir file('src/ratpack/assets/scripts')
}

prepareBaseDir {
  dependsOn gatherManuals, compileCoffee, compileSass
}

applicationDistribution.into("newrelic") {
  from({ configurations.newrelicAgent }) {
    rename { "agent.jar" }
  }
  from "src/newrelic"
}

compass {
  gemPath = new File(gradle.gradleUserHomeDir, ".jruby")
  cssDir = file('src/ratpack/assets/styles')
  sassDir = file('src/main/static/styles')
  imagesDir = file('src/ratpack/assets/images')
  importPath = files('../ratpack-stylesheets')
  fontsDir = file('../ratpack-stylesheets/fonts')
  relativeAssets = true
  quiet = true
}

compileSass {
  environment = 'production'
  debugInfo = false
}

clean.dependsOn cleanCompileSass
run.dependsOn watchSass

task createScriptsDir {
  doLast {
    mkdir "src/ratpack/assets/scripts"
  }
}
configure([watchSass, compileSass]) {
  dependsOn createScriptsDir
}

clean.dependsOn cleanCompileCoffee
clean.dependsOn cleanGatherManuals

idea {
  module {
    testSourceDirs += sourceSets.browserTest.allSource.srcDirs
    scopes.TEST.plus += [configurations.browserTestCompile]
  }
}