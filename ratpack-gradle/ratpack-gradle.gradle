/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.12.0"
  }
}

description = "Build time Ratpack support with the Gradle build tool - http://gradle.org"

apply from: "$rootDir/gradle/javaModule.gradle"
apply from: "$rootDir/gradle/versionFile.gradle"

configurations.testCompile.dependencies.clear()

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

dependencies {
  compile gradleApi()

  testCompile gradleTestKit()
  testCompile commonDependencies.guava
  testCompile 'org.spockframework:spock-core:1.2-groovy-2.5', {
    exclude module: "groovy-all", group: "org.codehaus.groovy"
  }
}

apiModules.each { evaluationDependsOn(it.path) }

test {
  dependsOn "publishAllPublicationsToProjectLocalRepository"
  dependsOn { apiModules*.publishAllPublicationsToProjectLocalRepository }
  onlyIf { !System.getenv('CI_GROOVY_VERSION') }
  allprojects {
    if (it == project) {
      return
    }
    mustRunAfter tasks.withType(VerificationTask)
  }
}

apply plugin: "com.gradle.plugin-publish"

pluginBundle {
  website = 'http://www.ratpack.io/'
  vcsUrl = 'https://github.com/ratpack/ratpack'
  tags = ['application', 'web', 'http', 'ratpack']

  plugins {
    ratpackBasePlugin {
      id = 'io.ratpack.ratpack-base'
      displayName = 'Ratpack Base Gradle Plugin'
      description = 'This plugin provides the base DSL extension useful for declaring Ratpack dependencies.'
      tags = ['java', 'application', 'web', 'http', 'ratpack']
    }

    ratpackJavaPlugin {
      id = 'io.ratpack.ratpack-java'
      displayName = 'Ratpack Java Gradle Plugin'
      description = 'This plugin provides build time integration for Java based Ratpack applications.'
      tags = ['java', 'application', 'web', 'http', 'ratpack']
    }

    ratpackGroovyPlugin {
      id = 'io.ratpack.ratpack-groovy'
      displayName = 'Ratpack Groovy Gradle Plugin'
      description = 'This plugin provides build time integration for Groovy based Ratpack applications.'
      tags = ['groovy', 'application', 'web', 'http', 'ratpack']
    }
  }
}

tasks.named("publishPlugins") {
  it.doFirst {
    if (isSnapshot) {
      throw new GradleException("Cannot publish snapshot Gradle plugin (version is ${project.version})")
    }
  }
}

test.doFirst {
  maxParallelForks 1
}
